use std
use sys

use "atomic"
use "common"

pkg thread =
	type sem = struct
		_val : uint64
	;;

	const mksem : (v : uint32 -> sem)
	const semwait : (s : sem# -> void)
	const semtrywait : (s : sem# -> bool)
	const sempost : (s : sem# -> void)
;;

const mksem = {v
	-> [._val = (v : uint64)]
}

const semwait = {s
	var v = 0

	for ; ;
		while (v = s._val) > 0
			if xcas(&s._val, v, v - 1) == v
				-> void
			;;
		;;
		sys.ulock_wait(sys.Ulockcompareandwait | sys.Ulocknoerrno, &s._val, v, 0)
	;;
	-> void /* Unreachable */
}

const semtrywait = {s
	for ; ;
		var v = xget(&s._val)
		if v == 0
			-> false
		;;
		if xcas(&s._val, v, v - 1) == v
			-> true
		;;
	;;
	-> false /* Unreachable */
}

const sempost = {s
	std.assert(xadd(&s._val, 1) != ~0x0, "error: semaphore overflowed\n")

	/* Unconditionally wake one waiter */
	sys.ulock_wake(sys.Ulockcompareandwait | sys.Ulocknoerrno, &s._val, 0)
}
